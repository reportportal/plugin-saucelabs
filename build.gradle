plugins {
    id 'net.researchgate.release' version '2.8.0'
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'java'
apply from: 'build-quality.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/release-commons.gradle'

project.ext {
    releaseMode = project.hasProperty("releaseMode")
    pluginID = "saucelabs"
    pluginsDir = "$buildDir/plugins"
}

version = '1.0.9'

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/epam/reportportal" }

    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }
}

dependencies {
    if (releaseMode) {
        compile 'com.github.reportportal:plugin-api:5.0.0-BETA-6'
    } else {
        compile 'com.github.reportportal:plugin-api:a60d458'
    }
    compile 'com.saucelabs:saucerest:1.0.40'
}


jar {
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '),
                "Plugin-Id": "${pluginID}",
                "Plugin-Version": "${project.version}",
                "Plugin-Provider": "Pavel Bortnik",
                "Plugin-Class": "com.epam.reportportal.saucelabs.SaucelabsPlugin",
//                "Plugin-Dependencies": ""
        )
    }
}

task plugin(type: Jar) {
    baseName = "plugin-${pluginID}"
    into('classes') {
        with jar
    }
    into('lib') {
        from configurations.compile
    }
    extension('zip')
}

task assemblePlugin(type: Copy) {
    from plugin
    into pluginsDir
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}

wrapper {
    gradleVersion = '5.4.1'
}