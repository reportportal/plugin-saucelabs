plugins {
    id 'net.researchgate.release' version '2.8.0'
    id "com.jfrog.bintray" version "1.8.4"
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/build-quality.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/release-fat.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

project.ext {
    releaseMode = project.hasProperty("releaseMode")
    pluginID = "saucelabs"
    pluginsDir = "$buildDir/plugins"
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/epam/reportportal" }
    maven { url 'https://jitpack.io' }
}

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:plugin-api:5.0.0-RC-5'
        annotationProcessor 'com.epam.reportportal:plugin-api:5.0.0-RC-5'
    } else {
        implementation 'com.epam.reportportal:plugin-api:5.0.0-RC-5'
        annotationProcessor 'com.epam.reportportal:plugin-api:5.0.0-RC-5'
    }
    implementation 'com.saucelabs:saucerest:1.0.43'
}


jar {
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '),
                "Plugin-Id": "${pluginID}",
                "Plugin-Version": "${project.version}",
                "Plugin-Provider": "Pavel Bortnik",
                "Plugin-Class": "com.epam.reportportal.saucelabs.SaucelabsPlugin",
//                "Plugin-Dependencies": ""
        )
    }
}

shadowJar {
    zip64 true
    dependencies {
        include(dependency('com.saucelabs:saucerest:1.0.43'))
        include(dependency('org.json:json:20171018'))
    }
}

task plugin(type: Jar) {
    baseName = "plugin-${pluginID}"
    into('classes') {
        with jar
    }
    into('lib') {
        from configurations.compile
    }
    extension('zip')
}

task assemblePlugin(type: Copy) {
    from plugin
    into pluginsDir
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}

wrapper {
    gradleVersion = '5.4.1'
}