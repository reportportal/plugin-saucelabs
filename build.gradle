plugins {
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id 'java'
}

project.ext {
    publishRepo = "https://maven.pkg.github.com/reportportal/plugin-saucelabs"
    dependencyRepos = ["plugin-api", "commons-bom"]
    releaseMode = project.hasProperty("releaseMode")
    pluginID = "saucelabs"
    pluginsDir = "$buildDir/plugins"
}

def scriptsUrl = 'https://raw.githubusercontent.com/reportportal/gradle-scripts/' +
        (releaseMode ? getProperty('scripts.version') : 'develop')

apply from: scriptsUrl + '/release-fat.gradle'
apply from: scriptsUrl + '/signing.gradle'
apply from: scriptsUrl + '/build-quality.gradle'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    maven { url "https://m2proxy.atlassian.com/repository/public" }
    mavenCentral { url "https://repo1.maven.org/maven2" }

    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }
}

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + getProperty('bom.version') : 'com.epam.reportportal:commons-bom:5.12.1')
    }
}

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:commons-dao'
        implementation 'com.epam.reportportal:plugin-api'
        annotationProcessor 'com.epam.reportportal:plugin-api'
    } else {
        implementation 'com.github.reportportal:commons-dao:acf1ec7'
        implementation 'com.github.reportportal:plugin-api:188792e'
        annotationProcessor 'com.github.reportportal:plugin-api:188792e'
    }
    implementation 'org.hibernate:hibernate-core:5.6.15.Final'

    // TODO: 2.5.3+ switched to camel-case models. UI updates required
    implementation 'com.saucelabs:saucerest:2.5.1'
    implementation 'dev.failsafe:failsafe:3.3.2'
    implementation 'org.awaitility:awaitility:4.2.2'
    implementation 'org.hamcrest:hamcrest-core:2.2'
    runtimeOnly 'com.squareup.moshi:moshi:1.15.1'

    // add lombok support
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

}

generatePomFileForShadowPublication { pom.packaging = "jar" }

jar {
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '),
                "Plugin-Id": "${pluginID}",
                "Plugin-Version": "${project.version}",
                "Plugin-Provider": "Pavel Bortnik",
                "Plugin-Class": "com.epam.reportportal.saucelabs.SaucelabsPlugin",
//                "Plugin-Dependencies": ""
        )
    }
}

shadowJar {
    archiveClassifier.set(null)
    zip64 true
    dependencies {
        include(dependency('com.saucelabs:saucerest:2.5.1'))
        include(dependency('org.json:json:20240303'))
        include(dependency('org.awaitility:awaitility:4.2.2'))
        include(dependency('org.hamcrest:hamcrest:2.2'))
        include(dependency('com.squareup.moshi:moshi:1.15.1'))
        include(dependency('net.jodah:failsafe:2.4.4'))

    }
}

task plugin(type: Jar) {
    baseName = "plugin-${pluginID}"
    into('classes') {
        with jar
    }
    into('lib') {
        from configurations.compile
    }
    extension('zip')
}

task assemblePlugin(type: Copy) {
    from plugin
    into pluginsDir
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}

wrapper {
    gradleVersion = '6.0'
}
