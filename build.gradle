plugins {
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id 'java'
}

project.ext {
    releaseMode = project.hasProperty("releaseMode")
    pluginID = "saucelabs"
    pluginsDir = "$buildDir/plugins"
}

def scriptsUrl = 'https://raw.githubusercontent.com/reportportal/gradle-scripts/' +
        (releaseMode ? getProperty('scripts.version') : 'develop')

apply from: scriptsUrl + '/release-fat.gradle'
apply from: scriptsUrl + '/build-quality.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    releaseMode ? maven { url "http://dl.bintray.com/epam/reportportal" } : maven { url 'https://jitpack.io' }
}

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + getProperty('bom.version') : 'com.github.reportportal:commons-bom:4b7ed8a')
    }
}

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:plugin-api'
        annotationProcessor 'com.epam.reportportal:plugin-api'
    } else {
        implementation 'com.github.reportportal:plugin-api:5.0.0-RC-5'
        annotationProcessor 'com.github.reportportal:plugin-api:5.0.0-RC-5'
    }
    implementation 'com.saucelabs:saucerest:1.0.43'
}


jar {
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '),
                "Plugin-Id": "${pluginID}",
                "Plugin-Version": "${project.version}",
                "Plugin-Provider": "Pavel Bortnik",
                "Plugin-Class": "com.epam.reportportal.saucelabs.SaucelabsPlugin",
//                "Plugin-Dependencies": ""
        )
    }
}

shadowJar {
    zip64 true
    dependencies {
        include(dependency('com.saucelabs:saucerest:1.0.43'))
        include(dependency('org.json:json:20171018'))
    }
}

task plugin(type: Jar) {
    baseName = "plugin-${pluginID}"
    into('classes') {
        with jar
    }
    into('lib') {
        from configurations.compile
    }
    extension('zip')
}

task assemblePlugin(type: Copy) {
    from plugin
    into pluginsDir
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}

wrapper {
    gradleVersion = '5.4.1'
}