plugins {
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id 'java'
}

project.ext {
    publishRepo = "https://maven.pkg.github.com/reportportal/plugin-saucelabs"
    dependencyRepos = ["plugin-api", "commons-bom"]
    releaseMode = project.hasProperty("releaseMode")
    pluginID = "saucelabs"
    pluginsDir = "$buildDir/plugins"
}

def scriptsUrl = 'https://raw.githubusercontent.com/reportportal/gradle-scripts/' +
        (releaseMode ? getProperty('scripts.version') : 'develop')

apply from: scriptsUrl + '/release-fat.gradle'
apply from: scriptsUrl + '/signing.gradle'
apply from: scriptsUrl + '/build-quality.gradle'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }
    mavenCentral { url "https://repo1.maven.org/maven2" }
}

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + getProperty('bom.version') : 'com.epam.reportportal:commons-bom:5.11.0')
    }
}

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:commons-dao'
        implementation 'com.epam.reportportal:plugin-api'
        annotationProcessor 'com.epam.reportportal:plugin-api'
    } else {
        implementation 'com.github.reportportal:commons-dao:28d0461'
        implementation 'com.github.reportportal:plugin-api:a9a8b73'
        annotationProcessor 'com.github.reportportal:plugin-api:a9a8b73'
    }
    implementation 'org.hibernate:hibernate-core:6.5.2.Final'

    // add lombok support
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

}

generatePomFileForShadowPublication { pom.packaging = "jar" }

jar {
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '),
                "Plugin-Id": "${pluginID}",
                "Plugin-Version": "${project.version}",
                "Plugin-Provider": "Pavel Bortnik",
                "Plugin-Class": "com.epam.reportportal.saucelabs.SaucelabsPlugin",
//                "Plugin-Dependencies": ""
        )
    }
}

shadowJar {
    archiveClassifier.set(null)
    zip64 true
    dependencies {
//        include(dependency('com.saucelabs:saucerest:2.5.1'))


    }
}

task plugin(type: Jar) {
    baseName = "plugin-${pluginID}"
    into('classes') {
        with jar
    }
    into('lib') {
        from configurations.compile
    }
    extension('zip')
}

task assemblePlugin(type: Copy) {
    from plugin
    into pluginsDir
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}

wrapper {
    gradleVersion = '6.0'
}
